River {
     Program = Decl* | space
   
     Decl = ModelDecl | VarDecl | TaskDecl | OutflowStmt | Stmt
   
     VarDecl = frozen? Type id "~" Exp  -- with_init
             | frozen? Type id          -- without_init
   
     
     TaskDecl = task id "(" Params ")" Block
     Params = ListOf<Param, ",">
     Param = Type? id ("~" Exp)?
     Block = Stmt*
     
     ModelDecl = model modelname Decl*
   
     Type = Type "[" "]"    -- array
          | Type "?"        -- optional
          | num              -- number
          | text          -- string
          | bool          -- boolean
          | stream			-- stream
          | modelname		-- model
          | frozen Type     -- frozen
   
     Stmt = VarDecl
          | Assignment 
          | CallStmt 
          | IndexStmt
          | IfStmt 
          | LoopStmt 
          | ReturnStmt
          | DryStmt
          | OutflowStmt
          | InflowStmt
          | dry
   
     Assignment = id "~" Exp
     DryStmt = dry
     InflowStmt = inflow "(" (stringlit | CallStmt)? ")"
     OutflowStmt = outflow "(" Exp ")"
     ContinueStmt = continue
     ReturnStmt = return Exp
     CallStmt = Call
     IndexStmt = Index
     
     LoopStmt = loop "(" Exp? ")" Block
     IfStmt = if Exp Block else Block  -- with_else
            | if Exp Block else IfStmt -- nested_if
            | if Exp Block             -- plain_if
     Call = id "(" Args ")"
     Index = id "[" Exp "]"
     Args = ListOf<Exp, ",">
   
     Exp  = "-" Primary  --negate
          | Exp0
     Exp0 = Exp0 ("||" | "??") Exp1  --ororcoalesce
          | Exp1
     Exp1 = Exp1 "&&" Exp2  --and
          | Exp2
     Exp2 = Exp3 relop Exp3  --compare
          | Exp3
     Exp3 = Exp3 ("+" | "-") Term --add
          | Term
     Term = Term ("*" | "/" | "%") Factor --mul
          | Factor
     Factor = Primary "**" Factor  -- power
            | Primary
     Primary = "(" Exp ")"              -- parens
             | "[" ListOf<Exp, ","> "]" -- array
             | ~num Primary "[" Exp "]" -- subscript
             | ~num Primary "." id      -- member
             | ~num Primary "?." id      -- unwrapmember
             | Call
             | Index
             | id
             | numlit
             | true
             | false
             | stringlit
             | InflowStmt
   
   
     relop = "<=" | "<" | ">=" | ">" | "==" | "!="
     access = public | private
     
     text = "text" ~idchar
     bool = "bool" ~idchar
     num = "num" ~idchar
     stream = "stream" ~idchar
     task = "task" ~idchar
     model = "model" ~idchar
     inflow = "inflow" ~idchar
     outflow = "outflow" ~idchar
     frozen = "frozen" ~idchar
     loop = "loop" ~idchar
     if = "if" ~idchar
     else = "else" ~idchar
     return = "return" ~idchar
     dry = "dry" ~idchar
     continue = "continue" ~idchar
     in = "in" ~idchar
     true = "true" ~idchar
     false = "false" ~idchar
     ctor = "constructor" ~idchar
     public = "public" ~idchar
     private = "private" ~idchar
     keyword = loop | if | else
             | return | dry | continue
             | true | false | text
             | bool | num | inflow | outflow
     id = ~keyword letter idchar*
     modelname = ~keyword upper idchar*
     idchar = alnum | "_"
     numlit = digit+ ("." digit+)? (("e" | "E") ("+" | "-")? digit+)?
   
     stringlit = "\"" char* "\""
     char = "\\u{" hexDigit+ "}"   --codepoint
          | "\\" any               --nonbackslash
          | ~"\"" any
          
     space += "//" (~"\n" any)*  --comment
}